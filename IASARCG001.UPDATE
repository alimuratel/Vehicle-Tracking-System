OBJECT: 
 INTEGER YENIKAYITVAR,
 INTEGER ITEMDURUM,
 INTEGER ARCDURUM,
 DOCUMENT DOCMNGREC,
 STRING IASDOCID2,
 STRING NEWDOCID;


IF IASARACHEAD_MULKIYET == 0 THEN
	MSJ = 'Lütfen mülkiyet alanını seçiniz.';
	MESSAGE IAS W0 WITH MSJ;
	RETURN;
ENDIF;


IF (IASARACHEAD_ARACTIPI == 2 
		|| IASARACHEAD_ARACTIPI == 4) 
		&& IASARACHEAD_MATERIAL == '' THEN
	MSJ= 'Lütfen üstyapı kodu seçiniz.';
	MESSAGE IAS W0 WITH MSJ;
	RETURN;
ENDIF;


IF IASARACHEAD_ARACTIPI == 4  THEN
	ITEMDURUM = 7;

	LOOP AT TBLARACITEM 
	BEGIN

		IF TBLARACITEM_ARACDURUMU != 7 THEN
			ITEMDURUM = TBLARACITEM_ARACDURUMU;
		ENDIF;

	ENDLOOP;


	IF ITEMDURUM == 7 THEN
		SELECT * 
			FROM IASARACHEAD 
			WHERE BAGLITANKNO = IASARACHEAD_TANKNO 
				AND BAGLITANKNO != '' 
			INTO TBLBAGLIKAMYON;


		IF SELECTED THEN
			MSJ = 'Teslim etmek istediğiniz üstyapı '+TBLBAGLIKAMYON_SASINO+' şasi numaralı kamyona bağlıdır.'+TOCHAR(10)+'İşlemin sonunda kamyonun teslimatı için ekrana yönlendirileceksiniz.'+TOCHAR(10)+' Emin misiniz?';
			MESSAGE IAS C0 WITH MSJ;

			IF CONFIRM == 'NO' THEN
				RETURN;
			ENDIF;

		ENDIF;

	ENDIF;

ENDIF;


IF IASARACHEAD_ARACTIPI == 3 
		&& P3 == 'KAMYONAC' THEN

	LOOP AT TBLARACITEM 
	BEGIN

		IF  TBLARACITEM_ARACDURUMU != 7 THEN
			MESSAGE IAS E0 WITH 'Lütfen müşteriye teslim işlemini tamamlayınız.';
			RETURN;
		ENDIF;

	ENDLOOP;

ENDIF;


IF THIS.HEADZORUNLUKONTROL() == 1 THEN
	RETURN;
ENDIF;

SELECT * 
	FROM IASUSERINFGRP 
	WHERE USERNAME = SYS_USER 
		AND INFOGRP IN ('D.SS01','D.SS02','D.PR00','D.PR02');


IF NOTSELECTED THEN
/*Kullanıcılar SSH grubunda değilse araç hareketi girmek zorundalar*/
/*SSH grubundakiler muayene merkezi için hareket olmaksızın kayıt yapabildikleri için bu kontrol var.*/
	IF TBLARACITEM_ROWCOUNT == 0 THEN
		MESSAGE IAS W0 WITH 'Lütfen araç hareketi giriniz.';
		RETURN;
	ENDIF;

ELSE

	IF TBLARACITEM_ROWCOUNT == 0 THEN
		SELECT * 
			FROM IASUSERINFGRP 
			WHERE USERNAME = SYS_USER 
				AND INFOGRP IN ('D.SS01');


		IF SELECTED THEN
			IASARACHEAD_ARACKAYNAGI = 'IASB';
		ENDIF;


		IF SELECTED THEN
			IASARACHEAD_ARACKAYNAGI = 'IASC';
			SETSTYLE BACKCOLOR KOYUIAS TO GRPMAIN;
		ENDIF;

	ENDIF;

ENDIF;


CODEREGION Özmal araç ise demirbaş kaydını kontrol et 
BEGIN

	IF IASARACHEAD_MULKIYET == 2 THEN

		IF IASARACHEAD_PLATE == '' THEN
			MESSAGE IAS W0 WITH 'Özmal aracı plakası olmadan kaydedemezsiniz!';
			SET DEMIRBASNO TO HIDE;
			SETSTYLE BACKCOLOR ZABOS TO IASARACHEAD_PLATE;
			RETURN;
		ENDIF;

		PLAKAKONTROL = 0;

		IF THIS.DEMIRBASKONTROL() == 1 THEN
			RETURN;
		ENDIF;

	ENDIF;

ENDREGION;


IF MODI == 1 THEN
	/*EKBELGE KONTROL BEGIN*/

	CODEREGION Ek Belge Kontrolü 
	BEGIN
		SELECT MAX(ITEMNUM) AS ITEMNUM 
			FROM IASARACITEM 
			WHERE DOCNUM = IASARACHEAD_DOCNUM 
			INTO TBLGUNCELKALEMKAYDI;

		YENIKAYITVAR = 0;

		CODEREGION Tescil ruhsatını tescil yeri veya müşterisi yurt dışı olan araçlardaki zorunluluğun kaldırılması 
		BEGIN

			LOOP AT  TBLARACITEM 
			BEGIN

				IF TBLARACITEM_INSERTITEM == 1 THEN
					YENIKAYITVAR = 1;
				ENDIF;

			ENDLOOP;


			IF YENIKAYITVAR == 0 THEN
				SELECT * 
					FROM IASSALITEM 
					WHERE CLIENT = SYS_CLIENT 
						AND DOCTYPE LIKE 'S%' 
						AND COMPANY = IASARACHEAD_ARACKAYNAGI 
						AND IASSASI = IASARACHEAD_SASINO 
					INTO TBLSATISSIPARIS;

				SELECT * 
					FROM IASSALITEM 
					WHERE CLIENT = SYS_CLIENT 
						AND DOCTYPE LIKE 'F%' 
						AND COMPANY = IASARACHEAD_ARACKAYNAGI 
						AND IASSASI = IASARACHEAD_SASINO 
					INTO TBLSATISFATURA;

				SELECT * 
					FROM IASSALHEAD 
					WHERE CLIENT = SYS_CLIENT 
						AND COMPANY = TBLSATISFATURA_COMPANY 
						AND DOCTYPE = TBLSATISFATURA_DOCTYPE 
						AND DOCNUM = TBLSATISFATURA_DOCNUM 
						AND CUSTOMER = IASARACHEAD_CUSTOMER 
					INTO TBLSATISFATURAHEAD;


				LOOP AT  TBLARACITEM 
				BEGIN

					IF TBLARACITEM_ITEMNUM == 1 
							&& TBLARACITEM_COMPANY == 'IAS' 
							&& TBLSATISFATURAHEAD_RADTESCILKIMDE != 2 
							&& TBLARACITEM_ARACDURUMU == 7 THEN
					ELSE

						IF TBLSATISSIPARIS_ROWCOUNT > 0 
								&& TBLSATISSIPARIS_EDTULKE2 != ''  THEN

							IF TBLARACITEM_ITEMNUM == TBLGUNCELKALEMKAYDI_ITEMNUM 
									&& TBLARACITEM_INSERTITEM == 0 
									&& TBLARACITEM_ARACDURUMU == 7 
									&& IASARACHEAD_ARACTIPI != 4 
									&& TBLSATISSIPARIS_EDTULKE2 == 'TR' THEN
								SELECT * 
									FROM IASDOCHEAD A 
									LEFT JOIN IASDOCITEM B ON (A.CLIENT = B.CLIENT 
										AND A.COMPANY = B.COMPANY 
										AND A.DOCTYPE = B.DOCTYPE 
										AND A.DOCID = B.DOCID) 
									WHERE A.CLIENT = SYS_CLIENT 
										AND A.COMPANY = 'IAS' 
										AND A.DOCTYPE = 'IASKP' 
										AND A.IASDOCID = IASARACHEAD_DOCNUM 
										AND B.BELGETURU = 'IASSAL01H009' 
									INTO TBLTESCILRUHSATVARMI;


								IF NOTSELECTED THEN
									MESSAGE IAS W0 WITH 'Araç Ek Belgelerine aracın tescil ruhsatını ekleyiniz.';
									RETURN;
								ENDIF;

							ENDIF;

						ENDIF;


						IF TBLSATISSIPARIS_ROWCOUNT == 0 
								|| TBLSATISSIPARIS_EDTULKE2 == '' THEN
							SELECT * 
								FROM IASCUSTOMER 
								WHERE CLIENT = SYS_CLIENT 
									AND COMPANY = 'IAS' 
									AND CUSTOMER = IASARACHEAD_CUSTOMER 
								INTO TBLMUSTERIULKE;


							IF TBLARACITEM_ITEMNUM == TBLGUNCELKALEMKAYDI_ITEMNUM 
									&& TBLARACITEM_INSERTITEM == 0 
									&& TBLARACITEM_ARACDURUMU == 7 
									&& IASARACHEAD_ARACTIPI != 4 
									&& TBLMUSTERIULKE_COUNTRY == 'TR' THEN
								SELECT * 
									FROM IASDOCHEAD A 
									LEFT JOIN IASDOCITEM B ON (A.CLIENT = B.CLIENT 
										AND A.COMPANY = B.COMPANY 
										AND A.DOCTYPE = B.DOCTYPE 
										AND A.DOCID = B.DOCID) 
									WHERE A.CLIENT = SYS_CLIENT 
										AND A.COMPANY = 'IAS' 
										AND A.DOCTYPE = 'IASKP' 
										AND A.IASDOCID = IASARACHEAD_DOCNUM 
										AND B.BELGETURU = 'IASSAL01H009' 
									INTO TBLTESCILRUHSATVARMI;


								IF NOTSELECTED THEN
									MESSAGE IAS W0 WITH 'Araç Ek Belgelerine aracın tescil ruhsatını ekleyiniz.';
									RETURN;
								ENDIF;

							ENDIF;

						ENDIF;

					ENDIF;

				ENDLOOP;

			ENDIF;

		ENDREGION;

	ENDREGION;

	MOVE SYS_USER TO IASARACHEAD_CHANGEDBY;
	MOVE SYS_CURRENTDATE TO IASARACHEAD_CHANGEDAT;
	BEGINTRAN;
	UPDATE IASARACHEAD 
		WHERE CLIENT = SYS_CLIENT 
			AND DOCNUM = IASARACHEAD_DOCNUM;


	IF SYS_STATUS THEN
		ROLLBACKTRAN;
		MESSAGE IAS E0 WITH 'IASARACHEAD Tablosu Güncelleme İşlemi Başarsız';
		RETURN;
	ENDIF;


	IF THIS.SAVEARACITEM() == 1 THEN
		RETURN;
	ENDIF;

	SELECT * 
		FROM IASARCITEMYAPILACAK 
		WHERE 1=2;


	IF YEDEKTBLARCITEMYAPILACAK_ROWCOUNT > 0 THEN

		LOOP AT YEDEKTBLARCITEMYAPILACAK 
		BEGIN
			DELETE 
				FROM IASARCITEMYAPILACAK 
				WHERE CLIENT = SYS_CLIENT 
					AND DOCNUM = YEDEKTBLARCITEMYAPILACAK_DOCNUM 
					AND ITEMNUM = YEDEKTBLARCITEMYAPILACAK_ITEMNUM;

		ENDLOOP;

	ENDIF;


	LOOP AT YEDEKTBLARCITEMYAPILACAK 
	BEGIN
		APPEND ROW TO IASARCITEMYAPILACAK;
		MOVE-CORRESPONDING YEDEKTBLARCITEMYAPILACAK TO IASARCITEMYAPILACAK;
		INSERT INTO IASARCITEMYAPILACAK ;

		IF SYS_STATUS > 0 THEN
			ROLLBACKTRAN;
			MESSAGE IAS E0 WITH 'Yapılacak işlemler kaydı başarsız';
			RETURN;
		ENDIF;

	ENDLOOP;

	SELECT MAX(ITEMNUM) AS ITEMNUM 
		FROM IASARACITEM 
		WHERE DOCNUM = IASARACHEAD_DOCNUM 
		INTO TBLMAXITEM;

	SELECT COMPANY, ARACDURUMU 
		FROM IASARACITEM 
		WHERE DOCNUM = IASARACHEAD_DOCNUM 
			AND ITEMNUM = TBLMAXITEM_ITEMNUM 
		INTO TBLSONKALEMBILGI;


	IF SELECTED THEN
		ARCDURUM = TBLSONKALEMBILGI_ARACDURUMU;
		IASARACHEAD_ARACKAYNAGI = TBLSONKALEMBILGI_COMPANY;
	ELSE
		ARCDURUM = 7;
		IASARACHEAD_ARACKAYNAGI = 'IAS';
	ENDIF;


	CODEREGION kamyon ve baglı tank  varsa ve musterısı değişirse 
	BEGIN

		IF IASARACHEAD_ARACTIPI == 3 
				&& IASARACHEAD_BAGLITANKNO != ''  THEN

			IF ILKARACSAHIBI != IASARACHEAD_CUSTOMER THEN
				UPDATE IASARACHEAD 
					SET CUSTOMER = IASARACHEAD_CUSTOMER 
					WHERE TANKNO = IASARACHEAD_BAGLITANKNO;


				IF SYS_STATUS THEN
					ROLLBACKTRAN;
				ENDIF;

				SELECT * 
					FROM IASARACSAHIPLOG 
					WHERE 1=2;

				SELECT * 
					FROM IASARACHEAD 
					WHERE TANKNO = IASARACHEAD_BAGLITANKNO 
					INTO USTYAPIGUNCELLE;

				APPEND ROW TO IASARACSAHIPLOG;
				IASARACSAHIPLOG_CLIENT = SYS_CLIENT;
				IASARACSAHIPLOG_DOCNUM = USTYAPIGUNCELLE_DOCNUM;
				SELECT MAX(ITEM) AS ITEM 
					FROM IASARACSAHIPLOG 
					WHERE CLIENT = SYS_CLIENT 
						AND DOCNUM = USTYAPIGUNCELLE_DOCNUM 
					INTO TBLMAXITEM;

				IASARACSAHIPLOG_MULKIYET = USTYAPIGUNCELLE_MULKIYET;
				IASARACSAHIPLOG_BAGLITANKNO = USTYAPIGUNCELLE_BAGLITANKNO;
				IASARACSAHIPLOG_ITEM = TBLMAXITEM_ITEM+1;
				IASARACSAHIPLOG_CHANGEDATE = SYS_CURRENTDATE;
				IASARACSAHIPLOG_CHANGEBY = SYS_USER;
				IASARACSAHIPLOG_TANKNO = USTYAPIGUNCELLE_TANKNO;
				IASARACSAHIPLOG_CUSTOMER = IASARACHEAD_CUSTOMER;
				IASARACSAHIPLOG_PLATE = USTYAPIGUNCELLE_PLATE;
				IASARACSAHIPLOG_MOTORNO = USTYAPIGUNCELLE_MOTORNO;
				IASARACSAHIPLOG_TESCIL = USTYAPIGUNCELLE_TESCIL;
				IASARACSAHIPLOG_RENK = USTYAPIGUNCELLE_RENK;
				IASARACSAHIPLOG_SASINO = IASARACHEAD_SASINO;
				IASARACSAHIPLOG_PLATE = IASARACHEAD_PLATE;
				IASARACSAHIPLOG_BAGLITANKNO = IASARACHEAD_BAGLITANKNO;
				INSERT INTO IASARACSAHIPLOG;

				IF SYS_STATUS THEN
					ROLLBACKTRAN;
				ENDIF;

			ENDIF;

		ENDIF;

	ENDREGION;


	IF IASARACHEAD_ARACTIPI == 3 
			&& ILKBAGLITANKNO != IASARACHEAD_BAGLITANKNO THEN

		IF IASARACHEAD_BAGLITANKNO != ''  THEN
			UPDATE IASARACITEM SET SALDOCITEM = (SELECT SALDOCITEM 
				FROM IASARACITEM 
				WHERE DOCNUM = IASARACHEAD_DOCNUM 
					AND ITEMNUM = (SELECT MAX(ITEMNUM) FROM IASARACITEM WHERE DOCNUM=IASARACHEAD_DOCNUM)), SALBASEDOCNUM = (SELECT SALBASEDOCNUM FROM IASARACITEM WHERE DOCNUM = IASARACHEAD_DOCNUM 
					AND ITEMNUM = (SELECT MAX(ITEMNUM) FROM IASARACITEM WHERE DOCNUM=IASARACHEAD_DOCNUM)), ISEMRINO = (SELECT ISEMRINO FROM IASARACITEM WHERE DOCNUM = IASARACHEAD_DOCNUM 
					AND ITEMNUM = (SELECT MAX(ITEMNUM) FROM IASARACITEM WHERE DOCNUM=IASARACHEAD_DOCNUM)) WHERE  DOCNUM = (SELECT DOCNUM FROM IASARACHEAD WHERE TANKNO = IASARACHEAD_BAGLITANKNO) 
					AND ITEMNUM = (SELECT MAX(ITEMNUM) AS ITEMNUM FROM IASARACITEM WHERE DOCNUM = (SELECT DOCNUM FROM IASARACHEAD WHERE TANKNO = IASARACHEAD_BAGLITANKNO));

		ELSE
			UPDATE IASARACITEM SET SALDOCITEM = '', SALBASEDOCNUM = '', ISEMRINO = '' WHERE  DOCNUM = (SELECT DOCNUM 
				FROM IASARACHEAD 
				WHERE TANKNO = ILKBAGLITANKNO) 
					AND ITEMNUM = (SELECT MAX(ITEMNUM) AS ITEMNUM FROM IASARACITEM WHERE DOCNUM = (SELECT DOCNUM FROM IASARACHEAD WHERE TANKNO = ILKBAGLITANKNO));

		ENDIF;

	ENDIF;

	UPDATE IASARACHEAD 
		SET ARACKAYNAGI = IASARACHEAD_ARACKAYNAGI, ARACDURUMU = ARCDURUM 
		WHERE CLIENT = SYS_CLIENT 
			AND DOCNUM = IASARACHEAD_DOCNUM  ;


	IF SYS_STATUS THEN
		ROLLBACKTRAN;
		MESSAGE IAS E0 WITH 'IASARACHEAD tablosu Araç Kaynağı ve Araç Durumu Güncelleme İşlemi Başarsız';
		RETURN;
	ENDIF;

	COMMITTRAN;
	ALERT 'Güncelleme İşlemi Başarılı' WITHLOCATION SOUTHWEST TIMEOUT 3000 ICON I;

	CODEREGION Müşteri Mülkiyeti 
	BEGIN

		IF ILKARACSAHIBI != IASARACHEAD_CUSTOMER 
				|| ILKPLAKA != IASARACHEAD_PLATE 
				|| ILKMOTORNO != IASARACHEAD_MOTORNO 
				|| ILKTESCIL != IASARACHEAD_TESCIL 
				|| ILKBAGLITANKNO != IASARACHEAD_BAGLITANKNO 
				|| ILKTANKNO != IASARACHEAD_TANKNO 
				|| ILKMULKIYET != IASARACHEAD_MULKIYET 
				|| ILKARACRENGI != IASARACHEAD_RENK THEN
			SELECT * 
				FROM IASARACSAHIPLOG 
				WHERE 1=2;

			BEGINTRAN;
			APPEND ROW TO IASARACSAHIPLOG;
			IASARACSAHIPLOG_CLIENT = SYS_CLIENT;
			IASARACSAHIPLOG_DOCNUM = IASARACHEAD_DOCNUM;
			SELECT MAX(ITEM) AS ITEM 
				FROM IASARACSAHIPLOG 
				WHERE CLIENT = SYS_CLIENT 
					AND DOCNUM = IASARACHEAD_DOCNUM 
				INTO TBLMAXITEM;

			IASARACSAHIPLOG_MULKIYET = IASARACHEAD_MULKIYET;
			IASARACSAHIPLOG_BAGLITANKNO = IASARACHEAD_BAGLITANKNO;
			IASARACSAHIPLOG_ITEM = TBLMAXITEM_ITEM + 1;
			IASARACSAHIPLOG_CHANGEDATE = SYS_CURRENTDATE;
			IASARACSAHIPLOG_CHANGEBY = SYS_USER;
			IASARACSAHIPLOG_TANKNO = IASARACHEAD_TANKNO;
			IASARACSAHIPLOG_CUSTOMER = IASARACHEAD_CUSTOMER;
			IASARACSAHIPLOG_PLATE = IASARACHEAD_PLATE;
			IASARACSAHIPLOG_MOTORNO = IASARACHEAD_MOTORNO;
			IASARACSAHIPLOG_TESCIL = IASARACHEAD_TESCIL;
			IASARACSAHIPLOG_RENK = IASARACHEAD_RENK;

			IF IASARACHEAD_ARACTIPI == 4 THEN
				SELECT * 
					FROM IASARACHEAD 
					WHERE BAGLITANKNO = IASARACHEAD_TANKNO 
					INTO TANKBILGILERI;

				IASARACSAHIPLOG_SASINO = TANKBILGILERI_SASINO;
				IASARACSAHIPLOG_PLATE = TANKBILGILERI_PLATE;
				IASARACSAHIPLOG_BAGLITANKNO = TANKBILGILERI_BAGLITANKNO;
			ENDIF;

			INSERT INTO IASARACSAHIPLOG;

			IF SYS_STATUS THEN
				ROLLBACKTRAN;
			ENDIF;


			IF ILKBAGLITANKNO != IASARACHEAD_BAGLITANKNO THEN
				SELECT * 
					FROM IASARACHEAD 
					WHERE (TANKNO = IASARACHEAD_BAGLITANKNO 
						OR TANKNO = ILKBAGLITANKNO) 
						AND TANKNO != '' 
					INTO TANKKODUVARMI;

				IASARACSAHIPLOG_RENK = TANKKODUVARMI_RENK;
				IASARACSAHIPLOG_DOCNUM = TANKKODUVARMI_DOCNUM;

				IF IASARACHEAD_BAGLITANKNO == '' THEN
					IASARACSAHIPLOG_PLATE = '';
					IASARACSAHIPLOG_MOTORNO = '';
				ENDIF;

				SELECT MAX(ITEM) AS ITEM 
					FROM IASARACSAHIPLOG 
					WHERE CLIENT = SYS_CLIENT 
						AND DOCNUM = TANKKODUVARMI_DOCNUM 
					INTO TBLMAXITEM;

				IASARACSAHIPLOG_ITEM = TBLMAXITEM_ITEM + 1;
				INSERT INTO IASARACSAHIPLOG;
			ENDIF;

			COMMITTRAN;
		ENDIF;

	ENDREGION;

	SELECT * 
		FROM IASARACHEAD 
		WHERE DOCNUM = IASARACHEAD_DOCNUM 
		INTO TBLUSYAPIKAMYON;

	SELECT * 
		FROM IASLOCKS 
		WHERE CLIENT = SYS_CLIENT 
			AND LOCKID LIKE 'AR' 
			AND LOCKKEYS LIKE IASARACHEAD_DOCNUM 
		INTO IASLOCKLST;

	LREC.DELETELOCK('AR', IASARACHEAD_DOCNUM);

	IF TBLUSYAPIKAMYON_ARACTIPI == 4 
			&& TBLUSYAPIKAMYON_ARACDURUMU == 7  THEN

		IF IASARACHEAD_TANKNO != '' THEN
			THIS.KAMYONKAYDIAC();
		ENDIF;

	ENDIF;


	IF ILKBAGLITANKNO != IASARACHEAD_BAGLITANKNO THEN
		THIS.USTYAPIMAIL();
	ENDIF;

ENDIF;


IF MODI == 0  THEN
	MOVE SYS_CLIENT TO IASARACHEAD_CLIENT;
	MOVE SYS_USER TO IASARACHEAD_CREATEDBY;
	MOVE SYS_CURRENTDATE TO IASARACHEAD_CREATEDAT;
	MOVE SYS_CURRENTDATE TO IASARACHEAD_CHANGEDAT;
	MOVE SYS_USER TO IASARACHEAD_CHANGEDBY;

	IF NRANGE.FETCHM('IAS', 'ARACNO') == 0 THEN
		RETURN;
	ENDIF;


	IF IASNUMRANGE_ISEXTERN == 0 THEN
		NEWRESNUM = NRANGE.NEWNUMBERS('IAS', 'ARACNO',SYS_CURRENTDATE);

		IF NEWRESNUM == NULL THEN
			MESSAGE SYS E009 WITH 'IAS' 'ARACNO';
			RETURN;
		ENDIF;

		MOVE NEWRESNUM TO IASARACHEAD_DOCNUM;
	ENDIF;

	BEGINTRAN;
	INSERT INTO IASARACHEAD ;

	IF SYS_STATUS > 0 THEN
		ROLLBACKTRAN;
		MESSAGE IAS E0 WITH 'IASARACHEAD Tablosuna Kayıt Ekleme İşlemi Başarsız';
		RETURN;
	ELSE

		IF IASARACHEAD_MULKIYET == 1 
				&& (IASARACHEAD_ARACTIPI == 1 
				|| IASARACHEAD_ARACTIPI == 2 
				|| IASARACHEAD_ARACTIPI == 3)  THEN
			THIS.IASSRVD5KADI();
		ENDIF;

		THIS.SAVEARACITEM();
		COMMITTRAN;
	ENDIF;

	ALERT 'Kaydetme İşlemi Başarılı' WITHLOCATION SOUTHWEST TIMEOUT 3000 ICON I;

	CODEREGION Müşteri Mülkiyeti 
	BEGIN

		IF ILKARACSAHIBI != IASARACHEAD_CUSTOMER 
				|| ILKPLAKA != IASARACHEAD_PLATE 
				|| ILKMOTORNO != IASARACHEAD_MOTORNO 
				|| ILKTESCIL != IASARACHEAD_TESCIL 
				|| ILKTANKNO != IASARACHEAD_TANKNO 
				|| ILKBAGLITANKNO != IASARACHEAD_BAGLITANKNO 
				|| ILKMULKIYET != IASARACHEAD_MULKIYET 
				|| ILKARACRENGI != IASARACHEAD_RENK THEN
			SELECT * 
				FROM IASARACSAHIPLOG 
				WHERE 1=2;

			BEGINTRAN;
			APPEND ROW TO IASARACSAHIPLOG;
			IASARACSAHIPLOG_CLIENT = SYS_CLIENT;
			IASARACSAHIPLOG_DOCNUM = IASARACHEAD_DOCNUM;
			SELECT MAX(ITEM) AS ITEM 
				FROM IASARACSAHIPLOG 
				WHERE CLIENT = SYS_CLIENT 
					AND DOCNUM = IASARACHEAD_DOCNUM 
				INTO TBLMAXITEM;

			IASARACSAHIPLOG_ITEM = TBLMAXITEM_ITEM + 1;
			IASARACSAHIPLOG_CHANGEDATE = SYS_CURRENTDATE;
			IASARACSAHIPLOG_CHANGEBY = SYS_USER;
			IASARACSAHIPLOG_CUSTOMER = IASARACHEAD_CUSTOMER;
			IASARACSAHIPLOG_PLATE = IASARACHEAD_PLATE;
			IASARACSAHIPLOG_RENK = IASARACHEAD_RENK;
			IASARACSAHIPLOG_TANKNO = IASARACHEAD_TANKNO;
			IASARACSAHIPLOG_MULKIYET = IASARACHEAD_MULKIYET;
			IASARACSAHIPLOG_BAGLITANKNO = IASARACHEAD_BAGLITANKNO;
			IASARACSAHIPLOG_MOTORNO = IASARACHEAD_MOTORNO;
			IASARACSAHIPLOG_TESCIL = IASARACHEAD_TESCIL;

			IF IASARACHEAD_ARACTIPI == 4 THEN
				SELECT * 
					FROM IASARACHEAD 
					WHERE BAGLITANKNO = IASARACHEAD_TANKNO 
					INTO TANKBILGILERI;

				IASARACSAHIPLOG_SASINO = TANKBILGILERI_SASINO;
				IASARACSAHIPLOG_PLATE = TANKBILGILERI_PLATE;
				IASARACSAHIPLOG_BAGLITANKNO = TANKBILGILERI_BAGLITANKNO;
			ENDIF;

			INSERT INTO IASARACSAHIPLOG;

			IF SYS_STATUS THEN
				ROLLBACKTRAN;
			ENDIF;


			IF ILKBAGLITANKNO != IASARACHEAD_BAGLITANKNO THEN
				SELECT * 
					FROM IASARACHEAD 
					WHERE (TANKNO = IASARACHEAD_BAGLITANKNO 
						OR TANKNO = ILKBAGLITANKNO) 
						AND TANKNO != '' 
					INTO TANKKODUVARMI;

				IASARACSAHIPLOG_RENK = TANKKODUVARMI_RENK;
				IASARACSAHIPLOG_DOCNUM = TANKKODUVARMI_DOCNUM;

				IF IASARACHEAD_BAGLITANKNO == '' THEN
					IASARACSAHIPLOG_PLATE = '';
					IASARACSAHIPLOG_MOTORNO = '';
				ENDIF;

				SELECT MAX(ITEM) AS ITEM 
					FROM IASARACSAHIPLOG 
					WHERE CLIENT = SYS_CLIENT 
						AND DOCNUM = TANKKODUVARMI_DOCNUM 
					INTO TBLMAXITEM;

				IASARACSAHIPLOG_ITEM = TBLMAXITEM_ITEM+1;
				INSERT INTO IASARACSAHIPLOG;
			ENDIF;

		ENDIF;

	ENDREGION;

	COMMITTRAN;
ENDIF;


CODEREGION SRVT05 Bilgilerini Güncelleme 
BEGIN
	BEGINTRAN;
	UPDATE IASMATINSTANCE 
		SET CUSTOMER = IASARACHEAD_CUSTOMER, GRCCUSTOMER = IASARACHEAD_CUSTOMER, IASPLAKA = IASARACHEAD_PLATE 
		WHERE INSTNUMBER = IASARACHEAD_SASINO;
/*
	UPDATE IASMATINSTANCEX 
		SET MATERIAL = IASARACHEAD_MATERIAL 
		WHERE INSTNUMBER = IASARACHEAD_SASINO;
*/

	IF SYS_STATUS THEN
		ROLLBACKTRAN;
		MESSAGE IAS E0 WITH 'SRVT05 Kaydı Güncellenemedi!';
		RETURN;
	ENDIF;

	COMMITTRAN;
ENDREGION;

SHUTDOWN;
